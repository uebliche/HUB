plugins {
    id 'xyz.jpenilla.run-paper'
    id 'com.modrinth.minotaur'
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${project.paperVersion}"
    implementation project(':common')
}

tasks.processResources {
    filesMatching("**/paper-plugin.yml") {
        expand(
                name: rootProject.name,
                version: rootProject.version,
                description: rootProject.description,
                apiVersion: "1.21"
        )
    }
}

tasks.jar {
    dependsOn 'processResources'
    archiveBaseName.set('hub')
    def targetVersion = (project.paperVersion ?: 'unknown').toString()
    def sanitizedVersion = targetVersion.replaceAll("[^A-Za-z0-9._-]", "-")
    archiveClassifier.set("paper-${sanitizedVersion}")
    manifest {
        attributes["paperweight-mappings-namespace"] = "mojang"
    }
    from(project(':common').sourceSets.main.output)
}

tasks.runServer {
    def forced = (project.findProperty("paperRunVersion") ?: System.getenv("PAPER_RUN_VERSION"))?.toString()
    if (forced) {
        minecraftVersion(forced)
        return
    }
    def targetVersion = (rootProject.hasProperty("paperGameVersions") && rootProject.paperGameVersions && !rootProject.paperGameVersions.isEmpty()) ? rootProject.paperGameVersions.last() : "latest"
    minecraftVersion(targetVersion)
}

modrinth {
    token = System.getenv("MODRINTH_API_TOKEN") ?: System.getenv("MODRINTH_TOKEN")
    projectId = "HrTclB8n"
    versionNumber = rootProject.version?.toString()
    changelog = System.getenv("CHANGELOG") ?: getChangelog()
    versionType = rootProject.versionType
    uploadFile = jar
    gameVersions = rootProject.paperGameVersions
    loaders = List.of("paper")
    syncBodyFrom = rootProject.file("README.md").text
}
