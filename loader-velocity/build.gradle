// Project: velocity
plugins {
    id 'xyz.jpenilla.run-velocity'
    id 'com.modrinth.minotaur'
}

// Dependencies
dependencies {
    implementation project(':common')
    compileOnly("com.velocitypowered:velocity-api:" + project.velocityVersion)
    annotationProcessor("com.velocitypowered:velocity-api:" + project.velocityVersion)
    compileOnly "net.kyori:adventure-text-minimessage:4.17.0"
    compileOnly "net.kyori:adventure-text-serializer-ansi:4.17.0"
    compileOnly "org.spongepowered:configurate-yaml:4.1.2"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

configurations.configureEach {
    attributes.attribute(org.gradle.api.attributes.java.TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 21)
}

// Velocity run configuration
tasks.runVelocity {
    println(project.velocityVersion)
    velocityVersion(project.velocityVersion)
}
tasks.jar {
    archiveBaseName.set('hub')
    def targetVersion = (project.velocityVersion ?: 'unknown').toString()
    def sanitizedVersion = targetVersion.replaceAll("[^A-Za-z0-9._-]", "-")
    archiveClassifier.set("velocity-${sanitizedVersion}")
    from(project(':common').sourceSets.main.output)
}


// Modrinth configuration
modrinth {
    token = System.getenv("MODRINTH_API_TOKEN") ?: System.getenv("MODRINTH_TOKEN")
    projectId = "HrTclB8n"
    versionNumber = rootProject.version?.toString()
    changelog = System.getenv("CHANGELOG") ?: getChangelog()
    versionType = rootProject.versionType
    uploadFile = jar
    gameVersions = rootProject.paperGameVersions
    loaders = List.of("velocity")
    syncBodyFrom = rootProject.file("README.md").text
}

