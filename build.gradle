plugins {
    id 'java'
    id 'eclipse'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
    id 'net.uebliche.mcmeta'
}

def resolvedTag = (project.findProperty('tag') ?: 'dev').toString()
def requestedMcVersion = (project.findProperty('mcVersion')
        ?: System.getenv('MC_VERSION')
        ?: System.getenv('mcVersion')
        ?: '').toString().trim()
if (requestedMcVersion.equalsIgnoreCase('latest')) {
    requestedMcVersion = ''
}

mcmeta {
    minecraftVersion = requestedMcVersion
    repositories {
        all()
    }
}
mcmeta.resolveNow(project)

def resolvedMcVersion = project.ext.has('mcmetaMinecraftVersion')
        ? project.ext.mcmetaMinecraftVersion?.toString()
        : null
if (!resolvedMcVersion) {
    throw new GradleException('mcmeta: minecraft version not resolved')
}

def resolvedVelocityVersion = project.ext.mcmetaVelocityVersion?.toString()
if (!resolvedVelocityVersion) {
    throw new GradleException("mcmeta: velocity version missing for ${resolvedMcVersion}")
}

def resolvedPaperVersion = project.ext.mcmetaPaperVersion?.toString()
if (!resolvedPaperVersion) {
    resolvedPaperVersion = "${resolvedMcVersion}-R0.1-SNAPSHOT"
}

def resolvedPaperGameVersions = [resolvedMcVersion]


description = 'Hub plugin for Velocity and Paper'
version = resolvedTag
group = 'net.uebliche'

ext {
    tag = resolvedTag
    version = resolvedTag
    versionType = "release"
    minecraftVersion = resolvedMcVersion
    velocityVersion = resolvedVelocityVersion
    paperVersion = resolvedPaperVersion
    paperGameVersions = resolvedPaperGameVersions
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'net.uebliche.mcmeta'

    group = 'net.uebliche'
    version = rootProject.version

    mcmeta {
        minecraftVersion = rootProject.ext.minecraftVersion
        repositories {
            all()
        }
    }

    ext {
        tag = rootProject.tag
        version = rootProject.version
        versionType = rootProject.versionType
        minecraftVersion = rootProject.minecraftVersion
        velocityVersion = rootProject.velocityVersion
        paperVersion = rootProject.paperVersion
        paperGameVersions = rootProject.paperGameVersions
    }

    def targetJavaVersion = 21
    java {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(targetJavaVersion)
    }
}
